defmodule Egregoros.PipelineTest do
  use Egregoros.DataCase, async: true

  alias Egregoros.Object
  alias Egregoros.Pipeline
  alias Egregoros.Relationships
  alias Egregoros.Users
  alias EgregorosWeb.Endpoint

  @note %{
    "id" => "https://example.com/objects/1",
    "type" => "Note",
    "attributedTo" => "https://example.com/users/alice",
    "content" => "Hello from pipeline"
  }

  @multi_type_note %{
    "id" => "https://example.com/objects/multi-type-note",
    "type" => ["Note", "OpenBadgeCredential"],
    "attributedTo" => "https://example.com/users/alice",
    "content" => "Hello from multi-type pipeline"
  }

  @create %{
    "id" => "https://example.com/activities/create/1",
    "type" => "Create",
    "actor" => "https://example.com/users/alice",
    "object" => @note
  }

  @multi_type_create %{
    "id" => "https://example.com/activities/create/multi-type",
    "type" => "Create",
    "actor" => "https://example.com/users/alice",
    "object" => @multi_type_note
  }

  @like %{
    "id" => "https://example.com/activities/like/1",
    "type" => "Like",
    "actor" => "https://example.com/users/alice",
    "object" => "https://example.com/objects/1"
  }

  @announce %{
    "id" => "https://example.com/activities/announce/1",
    "type" => "Announce",
    "actor" => "https://example.com/users/alice",
    "object" => "https://example.com/objects/1"
  }

  @follow %{
    "id" => "https://example.com/activities/follow/1",
    "type" => "Follow",
    "actor" => "https://example.com/users/alice",
    "object" => "https://example.com/users/bob"
  }

  @undo %{
    "id" => "https://example.com/activities/undo/1",
    "type" => "Undo",
    "actor" => "https://example.com/users/alice",
    "object" => "https://example.com/activities/follow/1"
  }

  @emoji_react %{
    "id" => "https://example.com/activities/react/1",
    "type" => "EmojiReact",
    "actor" => "https://example.com/users/alice",
    "object" => "https://example.com/objects/1",
    "content" => ":fire:"
  }

  test "ingest stores a Note as an object" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@note, local: true)
    assert object.ap_id == @note["id"]
    assert object.type == "Note"
    assert object.actor == @note["attributedTo"]
    assert object.data["content"] == "Hello from pipeline"
    assert object.local == true
  end

  test "ingest stores a multi-type Note while preserving canonical type array" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@multi_type_note, local: false)
    assert object.type == "Note"
    assert object.data["type"] == ["Note", "OpenBadgeCredential"]
    assert object.internal["auxiliary_types"] == ["OpenBadgeCredential"]
  end

  test "ingest stores Like" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@like, local: false)
    assert object.type == "Like"
    assert object.actor == @like["actor"]
    assert object.object == @like["object"]
    assert object.local == false
  end

  test "ingest accepts Like from followed actor even when not addressed directly to inbox user" do
    {:ok, user} = Users.create_local_user("inbox-user")
    actor_ap_id = "https://lain.com/users/lain"

    assert {:ok, _} =
             Relationships.upsert_relationship(%{
               type: "Follow",
               actor: user.ap_id,
               object: actor_ap_id,
               activity_ap_id: "https://egregoros.example/activities/follow/like-targeting"
             })

    like = %{
      "id" => "https://lain.com/activities/6c3ae6ff-e700-4890-834a-3ce143268fa2",
      "type" => "Like",
      "actor" => actor_ap_id,
      "object" => "https://clubcyberia.co/objects/c304b94e-83f9-43a5-8663-3444abb3bd57",
      "to" => ["https://clubcyberia.co/users/get", actor_ap_id <> "/followers"],
      "cc" => ["https://www.w3.org/ns/activitystreams#Public"]
    }

    assert {:ok, %Object{} = object} =
             Pipeline.ingest(like, local: false, inbox_user_ap_id: user.ap_id)

    assert object.type == "Like"
    assert object.actor == actor_ap_id
  end

  test "ingest stores Announce" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@announce, local: false)
    assert object.type == "Announce"
    assert object.actor == @announce["actor"]
    assert object.object == @announce["object"]
  end

  test "ingest stores Follow" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@follow, local: false)
    assert object.type == "Follow"
    assert object.actor == @follow["actor"]
    assert object.object == @follow["object"]
  end

  test "ingest stores Undo" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@undo, local: false)
    assert object.type == "Undo"
    assert object.actor == @undo["actor"]
    assert object.object == @undo["object"]
  end

  test "ingest stores EmojiReact" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@emoji_react, local: false)
    assert object.type == "EmojiReact"
    assert object.actor == @emoji_react["actor"]
    assert object.object == @emoji_react["object"]
    assert object.data["content"] == ":fire:"
  end

  test "ingest stores Create and its embedded object" do
    assert {:ok, %Object{} = object} = Pipeline.ingest(@create, local: false)
    assert object.type == "Create"
    assert object.actor == @create["actor"]
    assert object.object == @note["id"]

    assert Egregoros.Objects.get_by_ap_id(@note["id"])
  end

  test "ingest stores Create with a multi-type embedded object" do
    assert {:ok, %Object{} = _object} = Pipeline.ingest(@multi_type_create, local: false)

    embedded = Egregoros.Objects.get_by_ap_id(@multi_type_note["id"])
    assert %Object{} = embedded
    assert embedded.type == "Note"
    assert embedded.data["type"] == ["Note", "OpenBadgeCredential"]
    assert embedded.internal["auxiliary_types"] == ["OpenBadgeCredential"]
  end

  test "ingest stores embedded objects for announces even when the object isn't targeted to the inbox user" do
    {:ok, user} = Users.create_local_user("inbox-user")
    relay_actor = "https://relay.example/users/relay"

    assert {:ok, _} =
             Relationships.upsert_relationship(%{
               type: "Follow",
               actor: user.ap_id,
               object: relay_actor,
               activity_ap_id: "https://egregoros.example/activities/follow/1"
             })

    note_id = "https://remote.example/objects/1"

    announce = %{
      "id" => "https://relay.example/activities/announce/1",
      "type" => "Announce",
      "actor" => relay_actor,
      "to" => ["https://www.w3.org/ns/activitystreams#Public"],
      "object" => %{
        "id" => note_id,
        "type" => "Note",
        "actor" => "https://remote.example/users/alice",
        "content" => "Hello from the relay payload",
        "to" => ["https://www.w3.org/ns/activitystreams#Public"]
      }
    }

    assert {:ok, %Object{} = object} =
             Pipeline.ingest(announce, local: false, inbox_user_ap_id: user.ap_id)

    assert object.type == "Announce"
    assert object.object == note_id
    assert Egregoros.Objects.get_by_ap_id(note_id)
  end

  test "ingest accepts relay announces even when the follow is still pending" do
    {:ok, user} = Users.create_local_user("inbox-user")
    relay_actor = "https://relay.example/users/relay"

    assert {:ok, _} =
             Relationships.upsert_relationship(%{
               type: "FollowRequest",
               actor: user.ap_id,
               object: relay_actor,
               activity_ap_id: "https://egregoros.example/activities/follow/relay-pending"
             })

    note_id = "https://remote.example/objects/relay-note"

    announce = %{
      "id" => "https://relay.example/activities/announce/2",
      "type" => "Announce",
      "actor" => relay_actor,
      "to" => ["https://www.w3.org/ns/activitystreams#Public"],
      "object" => %{
        "id" => note_id,
        "type" => "Note",
        "actor" => "https://remote.example/users/alice",
        "content" => "Hello from the relay payload",
        "to" => ["https://www.w3.org/ns/activitystreams#Public"]
      }
    }

    assert {:ok, %Object{} = object} =
             Pipeline.ingest(announce, local: false, inbox_user_ap_id: user.ap_id)

    assert object.type == "Announce"
    assert object.object == note_id
  end

  test "ingest rejects unknown types" do
    assert {:error, :unknown_type} =
             Pipeline.ingest(%{"type" => "Wobble", "id" => "x"}, local: true)
  end

  test "ingest rejects empty content" do
    assert {:error, :invalid} =
             Pipeline.ingest(Map.put(@note, "content", "  "), local: true)
  end

  test "ingest rejects remote objects that claim a local id" do
    uuid = Ecto.UUID.generate()

    note =
      @note
      |> Map.put("id", Endpoint.url() <> "/objects/" <> uuid)
      |> Map.put("attributedTo", "https://example.com/users/alice")

    assert {:error, :local_id} = Pipeline.ingest(note, local: false)
  end

  test "ingest does not reject remote objects that use the same host but a different port" do
    uuid = Ecto.UUID.generate()

    note =
      @note
      |> Map.put("id", "http://localhost:5000/objects/" <> uuid)
      |> Map.put("attributedTo", "https://example.com/users/alice")

    assert {:ok, %Object{} = object} = Pipeline.ingest(note, local: false)
    assert object.ap_id == note["id"]
    assert object.local == false
  end
end
